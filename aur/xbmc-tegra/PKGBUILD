# $Id: PKGBUILD 58617 2011-11-16 15:35:16Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
# Contributor: Zeqadious <zeqadious.at.gmail.dot.com>
# Contributor: BlackEagle < ike DOT devolder AT herecura DOT be >
# Contributor: macau <tema.prokopenko@gmail.com> //bump to 11.0_rc2

_prefix=/usr
plugrel=1
pkgname=xbmc-tegra
pkgver=11.0
pkgrel=9
pkgdesc="A software media player and entertainment hub for digital media"
arch=('armv7h')
url="http://xbmc.org"
license=('GPL' 'custom')
depends=('bzip2' 'curl' 'enca' 'faac' 'faad2' 'fontconfig' 'fribidi'
        'glew' 'jasper' 'libcdio' 'libegl' 'libgl' 'libmad' 'libmms'
        'libmpeg2' 'libmysqlclient' 'libsamplerate' 'libxinerama'
        'libxrandr' 'libxtst' 'lzo2' 'sdl_image' 'sdl_mixer'
        'smbclient' 'wavpack' 'mesa-demos' 'xorg-xdpyinfo' 'libmicrohttpd'
        'libssh' 'libva' 'libmodplug' 'rtmpdump' 'lsb-release'
        'libpulse' 'sqlite3' 'libass')
makedepends=('boost' 'cmake' 'gperf' 'nasm' 'libvdpau' 'unzip' 'zip' 'libegl' 'mesa')
optdepends=('lirc: remote controller support'
        'unrar: access compressed files without unpacking them'
        'udisks: automount external drives'
        'upower: used to trigger suspend functionality'
        'libvdpau: for nvidia hardware acceleration'
        'lcms'
        'tk')
install="${pkgname}.install"
source=("http://mirrors.xbmc.org/releases/source/xbmc-$pkgver-Eden_rc2.tar.gz"
	'FEH.sh'
0001-Rebased-remove-asm-code-failing-on-hardfp-patch-by-a.patch
xbmc-11.0-remove-hardcoded-arm-compiler-options.patch
xbmc-11.0-libpng15.patch
xbmc-11.0-FEH-py.patch)

md5sums=('66ae950474aa9e70712fc820efc1cb67'
         '7127c0336f91ff48e408e2d1078fe8eb'
         '13211b164d66b3ec133921b9804401a5'
         '14598e905a3df67c702aee0a0f1dcbed'
         'd0e71629d294ca03ee15cd44f65c2e99'
         '3dbf26c3374fef87a8ee8222bb360e89')

build() {
  cd "${srcdir}/xbmc-$pkgver-Eden_rc2"
#fix gles issue on tegra boards
patch -p1 <$srcdir/0001-Rebased-remove-asm-code-failing-on-hardfp-patch-by-a.patch
patch -p1 <$srcdir/xbmc-11.0-remove-hardcoded-arm-compiler-options.patch
patch -p1 <$srcdir/xbmc-11.0-libpng15.patch
patch -p1 <$srcdir/xbmc-11.0-FEH-py.patch


LDFLAGS="-Wl,--hash-style=gnu"
export PYTHON="/usr/bin/python2.7"
  msg "Bootstrapping XBMC"
 ./bootstrap

#update python 
sed s/lpython2.5/lpython2.7/g -i Makefile.in

  msg "Configuring XBMC"
  
  
 ./configure --prefix=${_prefix} --disable-openmax --disable-neon --disable-vdpau --disable-pulse --disable-hal --disable-avahi --enable-external-libraries --disable-liba52 --disable-libdts --enable-external-python --disable-debug --enable-gles --disable-gl --disable-dvdcss --disable-joystick  --enable-tegra --disable-vaapi --disable-ccache --enable-external-ffmpeg --disable-optimizations --disable-optical-drive --disable-libbluray --disable-non-free --disable-faac
    
  # Now (finally) build
  msg "Running make"
  make
  make -C lib/addons/script.module.pil
  make -C lib/addons/script.module.pysqlite
}

package() {
  cd "${srcdir}/xbmc-$pkgver-Eden_rc2"
  msg "Running make install"
  make prefix="${pkgdir}${_prefix}" install

  # Replace FEH.py with FEH.sh (and thus remove external python dependency)
  install -D -m 0755 "${srcdir}/FEH.sh" "${pkgdir}${_prefix}/share/xbmc/FEH.sh"
  sed -i -e 's/^python \(.*\)FEH.py \(.*\)$/\1FEH.sh \2/' "${pkgdir}${_prefix}/bin/xbmc"

  # lsb_release fix
  sed -i -e 's/which lsb_release &> \/dev\/null/\[ -f \/etc\/arch-release ]/g' "${pkgdir}${_prefix}/bin/xbmc"
  sed -i -e "s/lsb_release -a 2> \/dev\/null | sed -e 's\/\^\/    \/'/cat \/etc\/arch-release/g" "${pkgdir}${_prefix}/bin/xbmc"

  # .desktop files
  install -D -m 0644 "${srcdir}/xbmc-$pkgver-Eden_rc2/tools/Linux/xbmc.desktop" "${pkgdir}${_prefix}/share/applications/xbmc.desktop"
  install -D -m 0644 "${srcdir}/xbmc-$pkgver-Eden_rc2/tools/Linux/xbmc-48x48.png" "${pkgdir}${_prefix}/share/pixmaps/xbmc.png"

  # Tools
  install -D -m 0755 "${srcdir}/xbmc-$pkgver-Eden_rc2/xbmc-xrandr" "${pkgdir}${_prefix}/share/xbmc/xbmc-xrandr"

  # Licenses
  install -d -m 0755 "${pkgdir}${_prefix}/share/licenses/${pkgname}"
  for licensef in LICENSE.GPL copying.txt; do
    mv "${pkgdir}${_prefix}/share/doc/xbmc/${licensef}" "${pkgdir}${_prefix}/share/licenses/${pkgname}"
  done

  # cleanup some stuff
  msg "Cleanup unneeded files"
  rm -rf "${pkgdir}/usr/share/xsessions"
  rm -f "${pkgdir}/usr/share/xbmc/FEH.py"
  rm -f "${pkgdir}/usr/share/icons/hicolor/icon-theme.cache"
}
# vim:set ts=2 sw=2 et:






